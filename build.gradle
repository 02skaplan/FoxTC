buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'http://maven.epoxide.xyz'
    }
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    ivy {
        name = "Railcraft"
        artifactPattern "http://www.railcraft.info/ivy/com.headlamp-games/Railcraft/[revision]/[module]_1.10.2-[revision]-[classifier].[ext]"
    }
}

ext.configFile = file "build.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.modVersion
group= "si.meansoft.traincraft"
archivesBaseName = "Traincraft"

minecraft {
    version = config.forgeVersion
    runDir = "run"
    mappings = config.mappings.toString()
    makeObfSourceJar = false
    useDepAts = true
    replaceIn "Traincraft.java"
    replace "@MODNAME@", config.modName.toString()
    replace "@VERSION@", config.modVersion.toString()
}

dependencies {
    deobfCompile "mezz.jei:jei_" + config.minecraftVersion.toString() + ":" + config.jeiVersion.toString()
    deobfCompile "net.darkhax.tesla:Tesla:" + config.minecraftVersion.toString() + "-" + config.teslaVersion.toString()
    deobfCompile "net.industrial-craft:industrialcraft-2:" + config.ic2Version.toString() + ":api"
    provided name: "Railcraft", version: config.railcraftVersion.toString(), classifier: "dev"
}

processResources {
    inputs.property "version", config.modVersion
    inputs.property "mcversion", config.minecraftVersion
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':config.modVersion, 'mcversion':config.minecraftVersion
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
